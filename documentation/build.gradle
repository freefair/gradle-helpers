plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'io.freefair.aggregate-javadoc'
    id 'io.freefair.javadoc-links'
}

configurations {
    asciidoctorExt
}

asciidoctor {
    baseDirFollowsSourceDir()

    attributes gradle_version: gradle.gradleVersion

    configurations 'asciidoctorExt'

    inputs.dir("src/docs/asciidoc")
}

dependencies {
    rootProject.subprojects { subproject ->
        subproject.plugins.withId("java") {
            javadoc subproject
        }
    }

    javadocClasspath "org.jacoco:org.jacoco.ant:${org.gradle.testing.jacoco.plugins.JacocoPlugin.DEFAULT_JACOCO_VERSION}"
    javadocClasspath "org.apache.maven.plugins:maven-plugin-plugin:3.10.1"
    javadocClasspath 'net.sourceforge.plantuml:plantuml:1.2023.12'

    asciidoctorExt "io.spring.asciidoctor:spring-asciidoctor-extensions-block-switch:0.6.2"
}

tasks.register("docsZip", Zip) {
    group = 'build'
    archiveClassifier = 'docs'
    dependsOn asciidoctor

    into("api") {
        from javadoc
    }
    into("reference") {
        from asciidoctor.outputDir
    }
    into("owasp") {
        from rootProject
                .files("build/reports/dependency-check-report.html")
                .builtBy(rootProject.tasks.named("dependencyCheckAggregate"))
    }
}

tasks.register("uploadDocs", io.freefair.gradle.plugins.okhttp.tasks.UploadFile) {
    dependsOn docsZip
    username = findProperty("freefairDocsUser")
    password = findProperty("freefairDocsPass")
    file = docsZip.archiveFile
    contentType = 'application/zip'
    url = "https://docs.freefair.io/api/$project.version?path=gradle-plugins"
}
